#
#  cyl-1.catt - A first cylinder example
#

import base

# Our example coherence.  
let excoh
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g : y -> z)
  : x -> z
  = comp3(f,g,id(z))

#  The generated coherences
coh excoh_coh_z
    (x : *) 
    (y : *) (f : x -> y)
    (z0 : *) (g : y -> z0)
    (z1 : *) (z : z0 -> z1)
  : comp(excoh(x,y,f,z0,g),z) ->
    excoh(x,y,f,z1,comp(g,z)) 

coh excoh_coh_g
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g0 : y -> z)
            (g1 : y -> z) (g : g0 -> g1)
  : excoh(x,y,f,z,g0) ->
    excoh(x,y,f,z,g1)
  
coh excoh_coh_y
    (x : *) 
    (y0 : *) (f : x -> y0)
    (y1 : *) (y : y0 -> y1)
    (z : *) (g : y1 -> z)
  : excoh(x,y0,f,z,comp(y,g)) ->
    excoh(x,y1,comp(f,y),z,g) 

coh excoh_coh_f
    (x : *)
    (y : *) (f0 : x -> y)
            (f1 : x -> y) (f : f0 -> f1)
    (z : *) (g : y -> z)
  : excoh(x,y,f0,z,g) ->
    excoh(x,y,f1,z,g)
            
coh excoh_coh_x
    (x0 : *)
    (x1 : *) (x : x0 -> x1)
    (y : *) (f : x1 -> y)
    (z : *) (g : y -> z)
  : excoh(x0,y,comp(x,f),z,g) ->
    comp(x,excoh(x1,y,f,z,g)) 

# Defining the cylinder ...
section
  (x0 : *) (x1 : *) (x : x0 -> x1)
  (y0 : *) (y1 : *) (y : y0 -> y1)
  (f0 : x0 -> y0) (f1 : x1 -> y1) (f : comp(f0,y) -> comp(x,f1))
  (z0 : *) (z1 : *) (z : z0 -> z1)
  (g0 : y0 -> z0) (g1 : y1 -> z1) (g : comp(g0,z) -> comp(y,g1)) where

  # Source 0-cylinder
  let cohcyl_src_0 : * = x0
  let cohcyl_src_1 : * = x1
  let cohcyl_src : cohcyl_src_0() -> cohcyl_src_1() = x

  # Target 0-cylinder
  let cohcyl_tgt_0 : * = z0
  let cohcyl_tgt_1 : * = z1
  let cohcyl_tgt : cohcyl_tgt_0() -> cohcyl_tgt_1() = z

  # The resulting 1-cylinder
  
  let cohcyl_0
    : cohcyl_src_0() -> cohcyl_tgt_0()
    = excoh(x0,y0,f0,z0,g0)

  let cohcyl_1
    : cohcyl_src_1() -> cohcyl_tgt_1()
    = excoh(x1,y1,f1,z1,g1)

  let cohcyl
    : comp(cohcyl_0(),cohcyl_tgt()) ->
      comp(cohcyl_src(),cohcyl_1())
    = vert5(
        excoh_coh_z(f0,g0,z),
        excoh_coh_g(f0,g),
        excoh_coh_y(f0,y,g1),
        excoh_coh_f(f,g1),
        excoh_coh_x(x,f1,g1)
      )

end
