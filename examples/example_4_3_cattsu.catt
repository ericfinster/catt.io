#
#  Eckmann-Hilton in Cattsu
#

coh id (x : *) : x -> x

coh comp
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g : y -> z)
  : x -> z

coh vert
    (x : *)
    (y : *) (f : x -> y)
    (g : x -> y) (a : f -> g)
    (h : x -> y) (b : g -> h)
  : f -> h

coh whisk_r
    (x : *)
    (y : *) (f : x -> y)
            (g : x -> y) (a : f -> g)
    (z : *) (h : y -> z)
  : comp(f,h) -> comp(g,h)

coh whisk_l (x : *)
            (y : *) (f : x -> y)
	    (z : *) (g : y -> z)
	            (h : y -> z) (a : g -> h)
  : comp(f,g) -> comp(f,h)

coh swap3 (x : *)
    	  (y : *) (f : x -> y)
	       	  (g : x -> y) (a : f -> g)
	  (z : *) (h : y -> z)
	          (k : y -> z) (b : h -> k)
  : vert(whisk_r(a,h),whisk_l(g,b)) ->
    vert(whisk_l(f,b),whisk_r(a,k))

let eh (x : *)
       (a : id(x) -> id(x))
       (b : id(x) -> id(x))
  : vert(a,b) -> vert(b,a)
  = swap3(a,b)
