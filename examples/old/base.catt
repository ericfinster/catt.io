#####################################################
#
#  base.catt - named versions of basic coherences
#
#####################################################

#
#  Identities
#

coh id
    (x : *)
  : x -> x

coh id1
    (x : *)
    (y : *) (f : x -> y)
  : f -> f

coh id2
    (x : *)
    (y : *) (f : x -> y)
            (g : x -> y) (a : f -> g)
  : a -> a


#
#  1d compositions
#

coh comp
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g : y -> z)
  : x -> z

coh comp3
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g : y -> z)
    (w : *) (h : z -> w)
  : x -> w

coh comp4
    (a : *)
    (b : *) (m : a -> b)
    (c : *) (n : b -> c)
    (d : *) (o : c -> d)
    (e : *) (p : d -> e)
  : a -> e

coh comp5
    (a : *)
    (b : *) (m : a -> b)
    (c : *) (n : b -> c)
    (d : *) (o : c -> d)
    (e : *) (p : d -> e)
    (f : *) (q : e -> f)
  : a -> f

#
#  2d compositions
#

coh vert
    (x : *)
    (y : *) (f : x -> y)
            (g : x -> y) (a : f -> g)
            (h : x -> y) (b : g -> h)
  : f -> h

coh vert3
    (x : *)
    (y : *) (f : x -> y)
            (g : x -> y) (a : f -> g)
            (h : x -> y) (b : g -> h)
            (i : x -> y) (c : h -> i)
  : f -> i

coh vert4
    (x : *)
    (y : *) (f : x -> y)
            (g : x -> y) (a : f -> g)
            (h : x -> y) (b : g -> h)
            (i : x -> y) (c : h -> i)
            (j : x -> y) (d : i -> j)
  : f -> j

coh vert5
    (x : *)
    (y : *) (f : x -> y)
            (g : x -> y) (a : f -> g)
            (h : x -> y) (b : g -> h)
            (i : x -> y) (c : h -> i)
            (j : x -> y) (d : i -> j)
            (k : x -> y) (e : j -> k)
  : f -> k 

coh horiz
    (x : *)
    (y : *) (f : x -> y)
            (g : x -> y) (a : f -> g)
    (z : *) (h : y -> z)
            (k : y -> z) (b : h -> k)
  : comp(f,h) -> comp(g,k)

coh whisk_r
    (x : *)
    (y : *) (f : x -> y)
            (g : x -> y) (a : f -> g)
    (z : *) (h : y -> z)
  : comp(f,h) -> comp(g,h)

coh whisk_l
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g : y -> z)
            (h : y -> z) (a : g -> h)
  : comp(f,g) -> comp(f,h)

#
#  3d compositions
#

coh 3vert
    (x : *)
    (y : *) (f : x -> y)
            (g : x -> y) (a : f -> g)
                         (b : f -> g) (m : a -> b)
                         (c : f -> g) (n : b -> c)
  : a -> c

coh 3vert3
    (x : *)
    (y : *) (f : x -> y)
            (g : x -> y) (a : f -> g)
                         (b : f -> g) (m : a -> b)
                         (c : f -> g) (n : b -> c)
                         (d : f -> g) (o : c -> d)
  : a -> d


#
#  Basic coherences
#

coh assoc_r
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g : y -> z)
    (w : *) (h : z -> w)
  : comp(comp(f,g),h) -> comp(f,comp(g,h))

coh assoc_l
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g : y -> z)
    (w : *) (h : z -> w)
  : comp(f,comp(g,h)) -> comp(comp(f,g),h) 

coh unit_l
    (x : *)
    (y : *) (f : x -> y)
  : f -> comp(id(x),f)

coh unit_r
    (x : *)
    (y : *) (f : x -> y)
  : f -> comp(f,id(y))
  
#
#  Some coherences used in cylinders
#

coh whisk_r_comp
    (x : *)
    (y : *) (f : x -> y)
            (g : x -> y) (a : f -> g)
    (z : *) (h : y -> z)
    (w : *) (k : z -> w)
  : vert(whisk_r(a,comp(h,k)),assoc_l(g,h,k)) ->
    vert(assoc_l(f,h,k),whisk_r(whisk_r(a,h),k))

coh whisk_r_fuse
    (x : *)
    (y : *) (f : x -> y)
            (g : x -> y) (a : f -> g)
            (h : x -> y) (b : g -> h)
    (z : *) (k : y -> z)
  : vert(whisk_r(a,k),whisk_r(b,k)) ->
    whisk_r(vert(a,b),k)

coh whisk_r_unfuse
    (x : *)
    (y : *) (f : x -> y)
            (g : x -> y) (a : f -> g)
            (h : x -> y) (b : g -> h)
    (z : *) (k : y -> z)
  : whisk_r(vert(a,b),k) ->
    vert(whisk_r(a,k),whisk_r(b,k))

coh whisk_l_comp
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g : y -> z)
    (w : *) (h : z -> w)
            (k : z -> w) (a : h -> k)
  : vert(whisk_l(f,whisk_l(g,a)),assoc_l(f,g,k)) ->
    vert(assoc_l(f,g,h),whisk_l(comp(f,g),a))

coh whisk_l_fuse
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g : y -> z)
            (h : y -> z) (a : g -> h)
            (k : y -> z) (b : h -> k)
  : vert(whisk_l(f,a),whisk_l(f,b)) ->
    whisk_l(f,vert(a,b))

coh whisk_l_unfuse
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g : y -> z)
            (h : y -> z) (a : g -> h)
            (k : y -> z) (b : h -> k)
  : whisk_l(f,vert(a,b)) ->
    vert(whisk_l(f,a),whisk_l(f,b)) 

coh whisk_lr
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g : y -> z)
            (h : y -> z) (a : g -> h)
    (w : *) (k : z -> w)
  : vert(whisk_r(whisk_l(f,a),k),assoc_r(f,h,k)) ->
    vert(assoc_r(f,g,k),whisk_l(f,whisk_r(a,k)))

coh comp310 (x : *)
            (y : *) (f : x -> y)
                    (g : x -> y) (a : f -> g)
                                 (b : f -> g) (m : a -> b)
            (z : *) (h : y -> z)
  : whisk_r(a,h) -> whisk_r(b,h)
  # comp210(a,h) -> comp210(b,h)

 
            
