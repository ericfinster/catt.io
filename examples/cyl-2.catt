#
#  cyl-2.catt - Another cylinder example
#

import base

#
# 1d Source Coherence
#

let src_coh
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g : y -> z)
  : x -> z    
  = comp3(f,id(y),g)

coh src_coh_z
    (x : *) 
    (y : *) (f : x -> y)
    (z0 : *) (g : y -> z0)
    (z1 : *) (z : z0 -> z1)
  : comp(src_coh(x,y,f,z0,g),z) ->
    src_coh(x,y,f,z1,comp(g,z)) 

coh src_coh_g
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g0 : y -> z)
            (g1 : y -> z) (g : g0 -> g1)
  : src_coh(x,y,f,z,g0) ->
    src_coh(x,y,f,z,g1)
  
coh src_coh_y
    (x : *) 
    (y0 : *) (f : x -> y0)
    (y1 : *) (y : y0 -> y1)
    (z : *) (g : y1 -> z)
  : src_coh(x,y0,f,z,comp(y,g)) ->
    src_coh(x,y1,comp(f,y),z,g) 

coh src_coh_f
    (x : *)
    (y : *) (f0 : x -> y)
            (f1 : x -> y) (f : f0 -> f1)
    (z : *) (g : y -> z)
  : src_coh(x,y,f0,z,g) ->
    src_coh(x,y,f1,z,g)
            
coh src_coh_x
    (x0 : *)
    (x1 : *) (x : x0 -> x1)
    (y : *) (f : x1 -> y)
    (z : *) (g : y -> z)
  : src_coh(x0,y,comp(x,f),z,g) ->
    comp(x,src_coh(x1,y,f,z,g)) 

#
#  1d Target Coherence
#

let tgt_coh
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g : y -> z)
  : x -> z
  = comp(f,g)

coh tgt_coh_z
    (x : *) 
    (y : *) (f : x -> y)
    (z0 : *) (g : y -> z0)
    (z1 : *) (z : z0 -> z1)
  : comp(tgt_coh(x,y,f,z0,g),z) ->
    tgt_coh(x,y,f,z1,comp(g,z)) 

coh tgt_coh_g
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g0 : y -> z)
            (g1 : y -> z) (g : g0 -> g1)
  : tgt_coh(x,y,f,z,g0) ->
    tgt_coh(x,y,f,z,g1)
  
coh tgt_coh_y
    (x : *) 
    (y0 : *) (f : x -> y0)
    (y1 : *) (y : y0 -> y1)
    (z : *) (g : y1 -> z)
  : tgt_coh(x,y0,f,z,comp(y,g)) ->
    tgt_coh(x,y1,comp(f,y),z,g) 

coh tgt_coh_f
    (x : *)
    (y : *) (f0 : x -> y)
            (f1 : x -> y) (f : f0 -> f1)
    (z : *) (g : y -> z)
  : tgt_coh(x,y,f0,z,g) ->
    tgt_coh(x,y,f1,z,g)
            
coh tgt_coh_x
    (x0 : *)
    (x1 : *) (x : x0 -> x1)
    (y : *) (f : x1 -> y)
    (z : *) (g : y -> z)
  : tgt_coh(x0,y,comp(x,f),z,g) ->
    comp(x,tgt_coh(x1,y,f,z,g)) 

#
#  2d Coherences
#

coh thecoh
    (x : *)
    (y : *) (f : x -> y)
            (g : x -> y) (a : f -> g)
    (z : *) (h : y -> z)
            (k : y -> z) (b : h -> k)
  : src_coh(x,y,f,z,h) ->
    tgt_coh(x,y,g,z,k)

coh thecoh_z
    (x : *)
    (y : *) (f : x -> y)
            (g : x -> y) (a : f -> g)
    (z0 : *) (h : y -> z0)
             (k : y -> z0) (b : h -> k)
    (z1 : *) (z : z0 -> z1)
  : vert(whisk_r(thecoh(a,b),z),tgt_coh_z(g,k,z)) ->
    vert(src_coh_z(f,h,z),thecoh(a,whisk_r(b,z)))

coh thecoh_k
    (x : *)
    (y : *) (f : x -> y)
            (g : x -> y) (a : f -> g)
    (z : *) (h : y -> z)
            (k0 : y -> z) (b : h -> k0)
            (k1 : y -> z) (k : k0 -> k1)
  : vert(thecoh(a,b),tgt_coh_g(g,k)) ->
    thecoh(a,vert(b,k))

coh thecoh_b
    (x : *)
    (y : *) (f : x -> y)
            (g : x -> y) (a : f -> g)
    (z : *) (h : y -> z)
            (k : y -> z) (b0 : h -> k)
                         (b1 : h -> k) (b : b0 -> b1)
  : thecoh(a,b0) ->
    thecoh(a,b1)

coh thecoh_h
    (x : *)
    (y : *) (f : x -> y)
            (g : x -> y) (a : f -> g)
    (z : *) (h0 : y -> z)
            (h1 : y -> z) (h : h0 -> h1)
            (k : y -> z) (b : h1 -> k)
  : thecoh(a,vert(h,b)) ->
    vert(src_coh_g(f,h),thecoh(a,b))

coh thecoh_y
    (x : *)
    (y0 : *) (f : x -> y0)
             (g : x -> y0) (a : f -> g)
    (y1 : *) (y : y0 -> y1)
    (z : *) (h : y1 -> z)
            (k : y1 -> z) (b : h -> k)
  : vert(thecoh(a,whisk_l(y,b)),tgt_coh_y(g,y,k)) ->
    vert(src_coh_y(f,y,h),thecoh(whisk_r(a,y),b))

coh thecoh_g
    (x : *)
    (y : *) (f : x -> y)
            (g0 : x -> y) (a : f -> g0)
            (g1 : x -> y) (g : g0 -> g1)
    (z : *) (h : y -> z)
            (k : y -> z) (b : h -> k)
  : vert(thecoh(a,b),tgt_coh_f(g,k)) ->
    thecoh(vert(a,g),b)

coh thecoh_a
    (x : *)
    (y : *) (f : x -> y)
            (g : x -> y) (a0 : f -> g)
                         (a1 : f -> g) (a : a0 -> a1)
    (z : *) (h : y -> z)
            (k : y -> z) (b : h -> k)
  : thecoh(a0,b) ->
    thecoh(a1,b)
  
coh thecoh_f
    (x : *)
    (y : *) (f0 : x -> y)
            (f1 : x -> y) (f : f0 -> f1)
            (g : x -> y) (a : f1 -> g)
    (z : *) (h : y -> z)
            (k : y -> z) (b : h -> k)
  : thecoh(vert(f,a),b) ->
    vert(src_coh_f(f,h),thecoh(a,b)) 

coh thecoh_x
    (x0 : *)
    (x1 : *) (x : x0 -> x1)
    (y : *) (f : x1 -> y)
            (g : x1 -> y) (a : f -> g)
    (z : *) (h : y -> z)
            (k : y -> z) (b : h -> k)
  : vert(thecoh(whisk_l(x,a),b),tgt_coh_x(x,g,k)) ->
    vert(src_coh_x(x,f,h),whisk_l(x,thecoh(a,b))) 

#
#  Composite Cylinder 
#

section
  (x0 : *) (x1 : *) (x : x0 -> x1)
  (y0 : *) (y1 : *) (y : y0 -> y1)
  (f0 : x0 -> y0) (f1 : x1 -> y1) (f : comp(f0,y) -> comp(x,f1))
  (g0 : x0 -> y0) (g1 : x1 -> y1) (g : comp(g0,y) -> comp(x,g1))
  (a0 : f0 -> g0) (a1 : f1 -> g1) (a : vert(whisk_r(a0,y),g) -> vert(f,whisk_l(x,a1)))
  (z0 : *) (z1 : *) (z : z0 -> z1)
  (h0 : y0 -> z0) (h1 : y1 -> z1) (h : comp(h0,z) -> comp(y,h1))
  (k0 : y0 -> z0) (k1 : y1 -> z1) (k : comp(k0,z) -> comp(y,k1))
  (b0 : h0 -> k0) (b1 : h1 -> k1) (b : vert(whisk_r(b0,z),k) -> vert(h,whisk_l(y,b1))) where

  # 0d Source Cylinder
  let cyl_0src_0 : * = x0
  let cyl_0src_1 : * = x1
  let cyl_0src : cyl_0src_0() -> cyl_0src_1() = x

  # 0d Target Cylinder
  let cyl_0tgt_0 : * = z0
  let cyl_0tgt_1 : * = z1
  let cyl_0tgt : cyl_0tgt_0() -> cyl_0tgt_1() = z 

  # 1d Source Cylinder
  let cyl_1src_0 : cyl_0src_0() -> cyl_0tgt_0()
    = src_coh(x0,y0,f0,z0,h0)
  let cyl_1src_1 : cyl_0src_1() -> cyl_0tgt_1()
    = src_coh(x1,y1,f1,z1,h1)
  let cyl_1src : comp(cyl_1src_0(),cyl_0tgt()) -> comp(cyl_0src(),cyl_1src_1())
    = vert5(
        src_coh_z(f0,h0,z),
        src_coh_g(f0,h),
        src_coh_y(f0,y,h1),
        src_coh_f(f,h1),
        src_coh_x(x,f1,h1)
      )

  # 1d Target Cylinder
  let cyl_1tgt_0 : cyl_0src_0() -> cyl_0tgt_0()
    = tgt_coh(x0,y0,g0,z0,k0)
  let cyl_1tgt_1 : cyl_0src_1() -> cyl_0tgt_1()
    = tgt_coh(x1,y1,g1,z1,k1)
  let cyl_1tgt : comp(cyl_1tgt_0(),cyl_0tgt()) -> comp(cyl_0src(),cyl_1tgt_1())
    = vert5(
        tgt_coh_z(g0,k0,z),
        tgt_coh_g(g0,k),
        tgt_coh_y(g0,y,k1),
        tgt_coh_f(g,k1),
        tgt_coh_x(x,g1,k1)
      )

  # 2d Cylinder
  let cyl_0 : cyl_1src_0() -> cyl_1tgt_0()
    = thecoh(a0,b0)
  let cyl_1 : cyl_1src_1() -> cyl_1tgt_1()
    = thecoh(a1,b1)

  # assoc
  sig first : vert(whisk_r(thecoh(a0,b0),z),vert5(tgt_coh_z(g0,k0,z),tgt_coh_g(g0,k),tgt_coh_y(g0,y,k1),tgt_coh_f(g,k1),tgt_coh_x(x,g1,k1))) -> 
              vert(vert(whisk_r(thecoh(a0,b0),z),tgt_coh_z(g0,k0,z)),vert4(tgt_coh_g(g0,k),tgt_coh_y(g0,y,k1),tgt_coh_f(g,k1),tgt_coh_x(x,g1,k1)))

  # using thecoh_z
  sig second : vert(vert(whisk_r(thecoh(a0,b0),z),tgt_coh_z(g0,k0,z)),vert4(tgt_coh_g(g0,k),tgt_coh_y(g0,y,k1),tgt_coh_f(g,k1),tgt_coh_x(x,g1,k1))) ->
               vert(vert(src_coh_z(f0,h0,z),thecoh(a0,whisk_r(b0,z))),vert4(tgt_coh_g(g0,k),tgt_coh_y(g0,y,k1),tgt_coh_f(g,k1),tgt_coh_x(x,g1,k1)))

  # assoc
  sig third : vert(vert(src_coh_z(f0,h0,z),thecoh(a0,whisk_r(b0,z))),vert4(tgt_coh_g(g0,k),tgt_coh_y(g0,y,k1),tgt_coh_f(g,k1),tgt_coh_x(x,g1,k1))) ->
              vert3(src_coh_z(f0,h0,z),vert(thecoh(a0,whisk_r(b0,z)),tgt_coh_g(g0,k)),vert3(tgt_coh_y(g0,y,k1),tgt_coh_f(g,k1),tgt_coh_x(x,g1,k1)))  

  # using thecoh_k
  sig fouth : vert3(src_coh_z(f0,h0,z),vert(thecoh(a0,whisk_r(b0,z)),tgt_coh_g(g0,k)),vert3(tgt_coh_y(g0,y,k1),tgt_coh_f(g,k1),tgt_coh_x(x,g1,k1))) ->
              vert3(src_coh_z(f0,h0,z),thecoh(a0,vert(whisk_r(b0,z),k)),vert3(tgt_coh_y(g0,y,k1),tgt_coh_f(g,k1),tgt_coh_x(x,g1,k1)))

  # using thecoh_b
  sig fifth : vert3(src_coh_z(f0,h0,z),thecoh(a0,vert(whisk_r(b0,z),k)),vert3(tgt_coh_y(g0,y,k1),tgt_coh_f(g,k1),tgt_coh_x(x,g1,k1))) ->
              vert3(src_coh_z(f0,h0,z),thecoh(a0,vert(h,whisk_l(y,b1))),vert3(tgt_coh_y(g0,y,k1),tgt_coh_f(g,k1),tgt_coh_x(x,g1,k1)))

  # using thecoh_h 
  sig sixth : vert3(src_coh_z(f0,h0,z),thecoh(a0,vert(h,whisk_l(y,b1))),vert3(tgt_coh_y(g0,y,k1),tgt_coh_f(g,k1),tgt_coh_x(x,g1,k1))) ->
              vert3(src_coh_z(f0,h0,z),vert(src_coh_g(f0,h),thecoh(a0,whisk_l(y,b1))),vert3(tgt_coh_y(g0,y,k1),tgt_coh_f(g,k1),tgt_coh_x(x,g1,k1)))

  # assoc
  sig seventh : vert3(src_coh_z(f0,h0,z),vert(src_coh_g(f0,h),thecoh(a0,whisk_l(y,b1))),vert3(tgt_coh_y(g0,y,k1),tgt_coh_f(g,k1),tgt_coh_x(x,g1,k1))) -> 
                vert3(vert(src_coh_z(f0,h0,z),src_coh_g(f0,h)),vert(thecoh(a0,whisk_l(y,b1)),tgt_coh_y(g0,y,k1)),vert(tgt_coh_f(g,k1),tgt_coh_x(x,g1,k1)))

  # using thecoh_y 
  sig eighth : vert3(vert(src_coh_z(f0,h0,z),src_coh_g(f0,h)),vert(thecoh(a0,whisk_l(y,b1)),tgt_coh_y(g0,y,k1)),vert(tgt_coh_f(g,k1),tgt_coh_x(x,g1,k1))) ->
               vert3(vert(src_coh_z(f0,h0,z),src_coh_g(f0,h)),vert(src_coh_y(f0,y,h1),thecoh(whisk_r(a0,y),b1)),vert(tgt_coh_f(g,k1),tgt_coh_x(x,g1,k1)))

  # assoc 
  sig ninth : vert3(vert(src_coh_z(f0,h0,z),src_coh_g(f0,h)),vert(src_coh_y(f0,y,h1),thecoh(whisk_r(a0,y),b1)),vert(tgt_coh_f(g,k1),tgt_coh_x(x,g1,k1))) ->
              vert3(vert3(src_coh_z(f0,h0,z),src_coh_g(f0,h),src_coh_y(f0,y,h1)),vert(thecoh(whisk_r(a0,y),b1),tgt_coh_f(g,k1)),tgt_coh_x(x,g1,k1))

  # using thecoh_g
  sig tenth : vert3(vert3(src_coh_z(f0,h0,z),src_coh_g(f0,h),src_coh_y(f0,y,h1)),vert(thecoh(whisk_r(a0,y),b1),tgt_coh_f(g,k1)),tgt_coh_x(x,g1,k1)) ->
              vert3(vert3(src_coh_z(f0,h0,z),src_coh_g(f0,h),src_coh_y(f0,y,h1)),thecoh(vert(whisk_r(a0,y),g),b1),tgt_coh_x(x,g1,k1))

  # using thecoh_a
  sig eleventh : vert3(vert3(src_coh_z(f0,h0,z),src_coh_g(f0,h),src_coh_y(f0,y,h1)),thecoh(vert(whisk_r(a0,y),g),b1),tgt_coh_x(x,g1,k1)) ->
                 vert3(vert3(src_coh_z(f0,h0,z),src_coh_g(f0,h),src_coh_y(f0,y,h1)),thecoh(vert(f,whisk_l(x,a1)),b1),tgt_coh_x(x,g1,k1))

  # using thecoh_f
  sig twelfth : vert3(vert3(src_coh_z(f0,h0,z),src_coh_g(f0,h),src_coh_y(f0,y,h1)),thecoh(vert(f,whisk_l(x,a1)),b1),tgt_coh_x(x,g1,k1)) ->
                vert3(vert3(src_coh_z(f0,h0,z),src_coh_g(f0,h),src_coh_y(f0,y,h1)),vert(src_coh_f(f,h1),thecoh(whisk_l(x,a1),b1)),tgt_coh_x(x,g1,k1))

  # assoc 
  sig thirteenth : vert3(vert3(src_coh_z(f0,h0,z),src_coh_g(f0,h),src_coh_y(f0,y,h1)),vert(src_coh_f(f,h1),thecoh(whisk_l(x,a1),b1)),tgt_coh_x(x,g1,k1)) ->
                   vert(vert4(src_coh_z(f0,h0,z),src_coh_g(f0,h),src_coh_y(f0,y,h1),src_coh_f(f,h1)),vert(thecoh(whisk_l(x,a1),b1),tgt_coh_x(x,g1,k1)))

  # using thecoh_x
  sig fourteenth : vert(vert4(src_coh_z(f0,h0,z),src_coh_g(f0,h),src_coh_y(f0,y,h1),src_coh_f(f,h1)),vert(thecoh(whisk_l(x,a1),b1),tgt_coh_x(x,g1,k1))) ->
                   vert(vert4(src_coh_z(f0,h0,z),src_coh_g(f0,h),src_coh_y(f0,y,h1),src_coh_f(f,h1)),vert(src_coh_x(x,f1,h1),whisk_l(x,thecoh(a1,b1))))

  # assoc
  sig fifteenth : vert(vert4(src_coh_z(f0,h0,z),src_coh_g(f0,h),src_coh_y(f0,y,h1),src_coh_f(f,h1)),vert(src_coh_x(x,f1,h1),whisk_l(x,thecoh(a1,b1)))) ->
                  vert(vert5(src_coh_z(f0,h0,z),src_coh_g(f0,h),src_coh_y(f0,y,h1),src_coh_f(f,h1),src_coh_x(x,f1,h1)),whisk_l(x,thecoh(a1,b1)))

  #
  #  And we're done.
  #

  # Ok! So the here's the final goal!
  sig cyl : vert(whisk_r(cyl_0(),cyl_0tgt()),cyl_1tgt()) ->
            vert(cyl_1src(),whisk_l(cyl_0src(),cyl_1()))
            
end
